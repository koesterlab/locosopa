from pathlib import Path
import yaml
import os
import hashlib
import json
import shutil
import tarfile
import urllib.request

page_config = yaml.load(open(config["page_config"]), Loader=yaml.SafeLoader)
page_config_dir = Path(config["page_config"]).parent
doc_config = page_config.get("docs", None)
assets = page_config.get("assets", [])

# Optional fediwall configuration: expects a key "fediwall_config" in page_config pointing
# to a JSON or YAML file with the wall configuration (title, subtitle, hashtags, etc.).
fediwall_config_rel = page_config.get("fediwall_config", None)
if fediwall_config_rel:
    fediwall_config_path = (page_config_dir / fediwall_config_rel).resolve()
    fediwall_enabled = fediwall_config_path.exists()
else:
    fediwall_config_path = None
    fediwall_enabled = False


rule all:
    input:
        # TODO add print_landscape once fixed
        expand(["build/{page}.html", "build/{page}.css"], page=["index", "print_landscape"]),
        expand("build/{asset}", asset=assets),
        "build/docs" if doc_config else [],
        "build/fediwall/wall-config.json" if fediwall_enabled else [],


rule build_css:
    input:
        "build/{page}.html",
        css="src/style.css",
    output:
        "build/{page}.css",
    conda:
        "envs/nodejs.yaml"
    shell:
        "npm install; npx tailwindcss -i {input.css} -o {output}"


rule build_html:
    input:
        config=config["page_config"],
        page="src/{page}.ytml.yaml",
    output:
        "build/{page}.html",
    wildcard_constraints:
        page="[^/]+",
    conda:
        "envs/ytml.yaml"
    shell:
        "ytml src build {input.config} {wildcards.page}.ytml.yaml"


rule build_docs:
    input:
        page_config=config["page_config"],
        src=lambda w: page_config_dir / page_config["docs"]["src"],
        sphinx_conf=workflow.source_path("../src/docs/conf.py"),
        docutils_conf=workflow.source_path("../src/docs/docutils.conf"),
        css=workflow.source_path("../src/docs/custom.css"),
    output:
        directory("build/docs"),
    conda:
        "envs/sphinx.yaml"
    params:
        page_config=lambda w, input: os.path.abspath(input.page_config),
        logos=[str(page_config_dir / logo) for logo in page_config["logo"].values()],
        js_assets=[str(page_config_dir / js) for js in page_config.get("javascript_assets", [])],
    shell:
        "tmpdir=$(mktemp -d); "
        "cp {input.sphinx_conf} $tmpdir; "
        "cp {input.docutils_conf} $tmpdir; "
        "mkdir $tmpdir/_static; "
        "cp {params.logos} {params.js_assets} $tmpdir/_static; "
        "cp {input.css} $tmpdir/_static; "
        "LOCOSOPA_CONFIG={params.page_config} sphinx-build -b html -c $tmpdir {input.src} {output}"


rule copy_asset:
    input:
        page_config_dir / "{asset}",
    output:
        "build/{asset}",
    shell:
        "mkdir -p $(dirname {output}); "
        "cp {input} {output}"


if fediwall_enabled:
    rule build_fediwall:
        input:
            config_source=str(fediwall_config_path)
        output:
            config="build/fediwall/wall-config.json",
            directory=directory("build/fediwall")
        params:
            version="v1.4.0",
            expected_sha256="9aeca19df37e1c872efa0e8047660eb15cfe24f8b14e5c198a20d966e7e456bc",
            config_source=lambda wildcards, input: input.config_source
        conda:
            "envs/fediwall.yaml"
        script:
            "scripts/fediwall.py"
